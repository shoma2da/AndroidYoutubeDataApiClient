apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1x'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:support-annotations:23.0.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.0"
    testCompile "org.mockito:mockito-core:1.+"
}

jacoco {
    version '0.7.5.201505241946'
}

def jacocoXmlReportPath = "build/reports/jacoco/xml/coverage.xml"

task unitTestCoverage(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    reports {
        html.enabled = true
        html {
            destination "build/reports/jacoco/html/"
        }
        xml.enabled = true
        xml {
            destination jacocoXmlReportPath
        }
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.class',
            ])
    sourceDirectories = files('src/main/java')
    executionData = files('build/jacoco/testDebugUnitTest.exec')
}

coveralls.jacocoReportPath = jacocoXmlReportPath

group = 'com.hatenablog.shoma2da.android'
version = '0.0.1'

def siteUrl = 'https://github.com/shoma2da/AndroidYoutubeDataApiClient'
def gitUrl = 'https://github.com/shoma2da/AndroidYoutubeDataApiClient.git'
bintray {
    user = "shoma2da"
    key = bintray_api_key ?: ""

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'AndroidYoutubeDataApiClient'
        desc = 'Simple client for YouTube Data API (v3) on Android.'
        websiteUrl = siteUrl
        issueTrackerUrl = '${siteUrl}/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['aar', 'android', 'example']
        publicDownloadNumbers = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
